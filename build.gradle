plugins {
    id 'com.github.johnrengelman.shadow' version '7.0.0' apply false
}

allprojects {
    apply plugin: 'java-library'
    apply plugin: 'com.github.johnrengelman.shadow'
    group = 'dev.rosewood'
    version = '1.2.6'
    targetCompatibility = 16
    sourceCompatibility = 16

    repositories {
        mavenCentral()

        maven { url = 'https://bitbucket.org/kangarko/libraries/raw/master' }
        maven { url = 'https://maven.enginehub.org/repo/' }
        maven { url = 'https://repo.md-5.net/content/repositories/public/' }
        maven { url = 'https://repo.codemc.org/repository/maven-public' }
        maven { url = 'https://repo.aikar.co/content/groups/aikar/' }
        maven { url = 'https://repo.extendedclip.com/content/repositories/placeholderapi/' }
        maven { url = 'https://maven.playpro.com' }
        maven { url = 'https://repo.viaversion.com/' }
        maven { url = 'https://repo.rosewooddev.io/repository/public/' }

        maven {
            url = 'https://repo.rosewooddev.io/repository/maven-public'
            credentials {
                username project.mavenUser
                password project.mavenPassword
                authentication {
                    basic(BasicAuthentication)
                }
            }
        }
    }
}

dependencies {
    implementation project(':Plugin')
    implementation project(':NMS-Wrapper')
    implementation project(':NMS-v1_13_R2')
    implementation project(':NMS-v1_14_R1')
    implementation project(':NMS-v1_15_R1')
    implementation project(':NMS-v1_16_R1')
    implementation project(':NMS-v1_16_R2')
    implementation project(':NMS-v1_16_R3')
    implementation project(':NMS-v1_17_R1')
}

shadowJar {
    archiveClassifier.set(null)

    relocate('dev.rosewood.rosegarden', 'dev.rosewood.rosestacker.lib.rosegarden')
    relocate('dev.rosewood.guiframework', 'dev.rosewood.rosestacker.lib.guiframework')
    relocate('co.aikar.commands', 'dev.rosewood.rosestacker.lib.acf.commands')
    relocate('co.aikar.locales', 'dev.rosewood.rosestacker.lib.acf.locales')

    exclude 'acf-*.properties'
}

build.dependsOn shadowJar
